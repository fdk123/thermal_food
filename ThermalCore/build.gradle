buildscript {
    repositories {
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${mc_version}-${mod_version}"
group = 'com.teamcofh'
archivesBaseName = "${mod_id}_foundation"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava { sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8 }

def setupSourceSet(SourceSet sourceSet, String name) {
    sourceSet.java {
        srcDir 'src/' + name + '/java'
    }
    sourceSet.compileClasspath += sourceSets.main.output
}

minecraft {
    mappings channel: 'snapshot', version: "${mcp_mappings}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    evaluationDependsOn(':CoFHCore')

    runs {
        data {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"

            args '--all', '--output', file('src/generated/'),
                    '--mod', archivesBaseName,
                    '--existing', sourceSets.main.resources.srcDirs[0]

            mods {
                CoFHCore {
                    source findProject(':CoFHCore').sourceSets.main
                }
                ThermalCore {
                    source findProject(':ThermalCore').sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { // TOP
        name 'tterrag maven'
        url 'https://maven.tterrag.com/'
    }
    maven { // JEI
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven { // Patchouli
        url 'https://maven.blamejared.com'
    }
    maven {
        url 'https://modmaven.dev'
    }
    maven {
        name 'CurseForge'
        url 'https://minecraft.curseforge.com/api/maven/'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")

    compileOnly fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.16.4:${crt_version}")

    if (findProject(':CoFHCore') != null) {
        compile project(':CoFHCore')
    } else {
        // TODO: Maven!
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "${mod_name}",
                "Specification-Vendor"    : "Team CoFH",
                "Specification-Version"   : "${mc_version}",
                "Implementation-Title"    : "${mod_id}",
                "Implementation-Vendor"   : "Team CoFH",
                "Implementation-Version"  : "${mod_version}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
